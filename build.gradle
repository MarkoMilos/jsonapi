buildscript {
  ext.versions = [
    'kotlin'         : '1.5.10',
    'moshi'          : '1.12.0',
    'javapoet'       : '1.13.0',
    'autoservice'    : '1.0',
    'junit'          : '4.13.2',
    'assertj'        : '3.19.0',
    'compile_testing': '0.19',
    'spotless'       : '5.7.0',
    'ktlint'         : '0.41.0'
  ]

  ext.deps = [
    'moshi'               : "com.squareup.moshi:moshi:${versions.moshi}",
    'moshi_kotlin_reflect': "com.squareup.moshi:moshi-kotlin:${versions.moshi}",
    'moshi_kotlin_codegen': "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}",
    'javapoet'            : "com.squareup:javapoet:${versions.javapoet}",
    'autoservice'         : "com.google.auto.service:auto-service:${versions.autoservice}",
    'junit'               : "junit:junit:${versions.junit}",
    'assertj'             : "org.assertj:assertj-core:${versions.assertj}",
    'compile_testing'     : "com.google.testing.compile:compile-testing:${versions.compile_testing}"
  ]

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:${versions.spotless}"
  }
}

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

apply plugin: "com.diffplug.spotless"
spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*", "**/generated/**/*.*"
    targetExclude "$buildDir/**/*.kt"
    targetExclude "bin/**/*.kt"
    // This should all come from editorconfig which is not currently supported
    // Related issue: https://github.com/diffplug/spotless/issues/142
    ktlint("0.41.0").userData([
      "indent_size"             : "2",
      "continuation_indent_size": "2"
    ])
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }
  java {
    target "**/src/**/*.java"
    removeUnusedImports()
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }
  groovyGradle {
    target "**/*.gradle"
    trimTrailingWhitespace()
    endWithNewline()
  }
}
