buildscript {
  ext.versions = [
    'kotlin'         : '1.4.32',
    'moshi'          : '1.12.0',
    'javapoet'       : '1.13.0',
    'autoservice'    : '1.0',
    'junit'          : '4.13.2',
    'assertj'        : '3.19.0',
    'compile_testing': '0.19',
    'spotless'       : '5.7.0',
    'ktlint'         : '0.41.0'
  ]

  ext.deps = [
    'kotlinStdlib'        : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
    'moshi'               : "com.squareup.moshi:moshi:${versions.moshi}",
    'moshi_kotlin_reflect': "com.squareup.moshi:moshi-kotlin:${versions.moshi}",
    'moshi_kotlin_codegen': "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}",
    'javapoet'            : "com.squareup:javapoet:${versions.javapoet}",
    'autoservice'         : "com.google.auto.service:auto-service:${versions.autoservice}",
    'junit'               : "junit:junit:${versions.junit}",
    'assertj'             : "org.assertj:assertj-core:${versions.assertj}",
    'compile_testing'     : "com.google.testing.compile:compile-testing:${versions.compile_testing}"
  ]

  repositories {
    mavenCentral()
    jcenter()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:${versions.spotless}"
  }
}

allprojects {
  repositories {
    mavenCentral()
    jcenter()
    google()
  }
}

apply plugin: "com.diffplug.spotless"
spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*", "**/generated/**/*.*"
    targetExclude "$buildDir/**/*.kt"
    targetExclude "bin/**/*.kt"
    // This should all come from editorconfig which is not currently supported
    // Related issue: https://github.com/diffplug/spotless/issues/142
    ktlint(versions.ktlint).userData([
      "indent_size"             : "2",
      "continuation_indent_size": "2"
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
  java {
    target "**/src/**/*.java"
    googleJavaFormat("1.7")
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
  }
  groovyGradle {
    target "**/*.gradle"
    trimTrailingWhitespace()
    endWithNewline()
  }
}
